<?xml version="1.0" encoding="utf-8"?>
<mx:WindowedApplication applicationComplete="appComplete()"  xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute">
	<mx:Script>
		<![CDATA[
			import flash.sampler.Sample;
			
			public static const PRELOADER_PATH:String = "C:/dev/yellowbadger/LeanAirProfiler/Preloader/bin-debug/Preloader.swf";
			
			public static const BEGIN_CHUNK:String = "BEGIN_CHUNKED_DATA";
			public static const END_CHUNK:String = "END_CHUNKED_DATA";
			
			private var securitySocket:ServerSocket = new ServerSocket();
			
			private var serverSocket:ServerSocket = new ServerSocket();
			
			private var clientSocket:Socket;
			
			private var buffer:String;
			
			private var buffering:Boolean = false;
			
			private function appComplete():void {
				securitySocket.bind(843);
				serverSocket.bind(9998);
				serverSocket.addEventListener(ServerSocketConnectEvent.CONNECT,onConnect);
				securitySocket.addEventListener(ServerSocketConnectEvent.CONNECT,onPolicyFileRequest);
				securitySocket.listen();
				writePreloaderConfig();
			}
			
			private function writePreloaderConfig():void {
				var f:File = File.userDirectory;
				var cfgFile:File = f.resolvePath("mm.cfg"); 
				
				var fs:FileStream = new FileStream();
				var ba:ByteArray = new ByteArray();
				if (cfgFile.exists) {
					fs.open(cfgFile,FileMode.READ);
					if (fs.bytesAvailable > 0) {
						fs.readBytes(ba,0,fs.bytesAvailable);
						var contents:String = ba.toString();
						if (contents.indexOf("PreloadSWF="+PRELOADER_PATH) >= 0) {
							fs.close();
							return;
						}
					}
					fs.close();
				}
				fs.open(cfgFile,FileMode.APPEND);
				var s:String = "PreloadSWF="+PRELOADER_PATH;
				ba = new ByteArray();
				ba.writeUTFBytes(s);
				fs.writeBytes(ba);
				fs.close();
			}
			
			private function erasePreloaderConfig():void {
				var f:File = File.userDirectory;
				var cfgFile:File = f.resolvePath("mm.cfg"); 
				if (!cfgFile.exists) {
					return;
				}
				var fs:FileStream = new FileStream();
				var contents:String;
				if (cfgFile.exists) {
					fs.open(cfgFile,FileMode.READ);
					if (fs.bytesAvailable > 0) {
						contents = fs.readUTF();
						if (contents.indexOf("PreloadSWF="+PRELOADER_PATH) == 0) {
							fs.close();
							return;
						}
					}
					fs.close();
				}
				fs.open(cfgFile,FileMode.WRITE);
				var pattern:RegExp = /PreloadSWF[^.]+.swf/gi
				contents.replace(pattern);
				var ba:ByteArray = new ByteArray();
				ba.writeUTFBytes(contents);
				fs.writeBytes(ba);
				fs.close();
			}
			
			
			private function onPolicyFileRequest(event:ServerSocketConnectEvent):void {
				event.socket.writeUTF('<cross-domain-policy><allow-access-from domain="*" to-ports="*" /></cross-domain-policy>');
				event.socket.flush();
				log("Got connection on port 843");
			}
			
			private function onConnect(event:ServerSocketConnectEvent):void {
				clientSocket = event.socket;
				clientSocket.addEventListener( ProgressEvent.SOCKET_DATA, onClientSocketData );
				log( "Connection from " + clientSocket.remoteAddress + ":" + clientSocket.remotePort );
				connected = true;
				if (autoStart.selected) {
					startProfiling();
				}
			}
			
			private function onClientSocketData( event:ProgressEvent ):void
			{
				var s:String = clientSocket.readUTFBytes(clientSocket.bytesAvailable);
				if (s.indexOf(BEGIN_CHUNK) == 0) {
					buffer = s.substr(BEGIN_CHUNK.length);
					buffering = true;
				} else if (s.indexOf(END_CHUNK) >= 0) {
					buffer += s.substr(0,s.length-END_CHUNK.length);
					log(buffer);
					buffer = "";
					buffering = false;
				} else {
					if (buffering) {
						buffer += s;
					}
					if (!buffering){
						log(s);
					}
				}
				
			}
			
			private function listen(event:TimerEvent = null):void {
				serverSocket.listen();
				listening = true;
			}
			
			private function stopListening():void {
				serverSocket.close();
				listening = false;
			}
			
			private function startProfiling():void {
				if (clientSocket  && clientSocket.connected) {
					clientSocket.writeUTFBytes("START");
					clientSocket.flush();
					profiling = true;
				}
			}
			
			private function pauseProfiling():void {
				if (clientSocket  && clientSocket.connected) {
					clientSocket.writeUTFBytes("PAUSE");
					clientSocket.flush();
				}
				profiling = false;
			}
			
			private function clearProfilingData():void {
				if (clientSocket  && clientSocket.connected) {
					clientSocket.writeUTFBytes("CLEAR");
					clientSocket.flush();
					profiling = false;
					logField.text = "";
					log("Profiling data cleared.");
				}
			}
			
			private function findObjects():void {
				if (clientSocket  && clientSocket.connected) {
					clientSocket.writeUTFBytes("READ_OBJECTS");
					clientSocket.flush();
				}
			}
			
			private function traceMethods():void {
				if (clientSocket  && clientSocket.connected) {
					clientSocket.writeUTFBytes("TRACE_METHODS");
					clientSocket.flush();
				}
			}
			
			private function log( text:String ):void
			{
				logField.text = text + "\n" + logField.text;
			}
			
			[Bindable]
			private var listening:Boolean = false;
			
			[Bindable]
			private var profiling:Boolean = false;
			
			[Bindable]
			private var connected:Boolean = false;
		]]>
	</mx:Script>
	<mx:HBox id="toolBar">
		<mx:Button label="Listen" toolTip="Open socket to listen for profiled apps." click="listen()" enabled="{!listening}" />
		<mx:Button label="Close" toolTip="Close the socket for the current app." click="stopListening()" enabled="{listening}" />
		<mx:Button label="Start" toolTip="Start collecting profiling data from the current app." click="startProfiling()" enabled="{!profiling &amp;&amp; connected}" />
		<mx:Button label="Pause" toolTip="Pause collecting profiling data from the current app." click="pauseProfiling()" enabled="{profiling &amp;&amp; connected}" />
		<mx:Button label="Clear" toolTip="Clear profiling data for the current app." click="clearProfilingData()" enabled="{connected}" />
		<mx:Button label="Read" toolTip="Read the current count of in memory objects from the profiler." click="findObjects()" enabled="{connected}" />
		<mx:Button label="Trace" toolTip="Trace method calls since profiling started." click="traceMethods()" enabled="{connected}" />
		<mx:CheckBox label="Autostart" id="autoStart" toolTip="Start profiling as soon as a client connects" />
	</mx:HBox>
	<mx:TextArea width="100%" top="{toolBar.height}" bottom="0" id="logField" wordWrap="false" />
</mx:WindowedApplication>
